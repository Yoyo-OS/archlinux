# Maintainer: Yoyo <admin@yzzi.icu>

pkgname=yoyo-libyoyo-git
_pkgname=libyoyo
pkgver=0.9
pkgrel=1
pkgdesc="System library for YoyoOS applications"
arch=('x86_64')
url="https://github.com/yoyo-os/libyoyo"
license=('GPL')
depends=('kscreen' 'modemmanager-qt' 'networkmanager-qt' 'qt5-quickcontrols2'
         # via dbus:
         'accountsservice' 'bluez' 'bluez-qt')
makedepends=('extra-cmake-modules' 'ninja' 'qt5-tools' 'git')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("git+$url.git")
sha512sums=('SKIP')

pkgver() {
  cd $_pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $_pkgname

  cmake -GNinja -DCMAKE_INSTALL_PREFIX=/usr .
  ninja
}

package() {
  cd $_pkgname
  DESTDIR="$pkgdir" ninja install
}